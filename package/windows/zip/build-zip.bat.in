@echo off

rem ...........................................................................

if "%1" == "" goto :release
if /i "%1" == "release" goto :release
if /i "%1" == "debug" goto :debug

echo Invalid configuration argument: "%1" (Release or Debug expected)
exit -1

:release
set CONFIGURATION=Release
goto :continue

:debug
set CONFIGURATION=Debug
goto :continue

:continue

rem ...........................................................................

set ROOT_DIR="${BULLDOZER_ROOT_DIR}"
set BIN_DIR="${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/%CONFIGURATION%"
set OUT_DIR="${BULLDOZER_PACKAGE_DIR}"
set ZIP_DIR="${CMAKE_CURRENT_BINARY_DIR}/zip"
set ZIP_EXE="${7Z_EXE}"

set ROOT_DIR=%ROOT_DIR:/=\%
set BIN_DIR=%BIN_DIR:/=\%
set OUT_DIR=%OUT_DIR:/=\%
set ZIP_DIR=%ZIP_DIR:/=\%
set ZIP_EXE=%ZIP_EXE:/=\%

set CONTENTS_DIR="%ZIP_DIR%\bulldozer-${VERSION_FILE_SUFFIX}"
set OUT_FILE_NAME=bulldozer-windows-${VERSION_FILE_SUFFIX}-${TARGET_CPU}.zip
set OUT_FILE_PATH=%OUT_DIR%\%OUT_FILE_NAME%

echo Building archive %OUT_FILE_NAME%...

if exist %ZIP_DIR% (rd /S /Q %ZIP_DIR% || exit)
if exist %OUT_FILE_PATH% (del %OUT_FILE_PATH%  || exit)

mkdir %CONTENTS_DIR%\bin || exit
mkdir %CONTENTS_DIR%\cmake || exit
mkdir %CONTENTS_DIR%\include || exit
mkdir %CONTENTS_DIR%\frame || exit
mkdir %CONTENTS_DIR%\samples || exit
if not exist "%OUT_DIR%" (mkdir "%OUT_DIR%" || exit)

xcopy /Y /Q %BIN_DIR%\bulldozer.exe %CONTENTS_DIR%\bin
xcopy /Y /Q %ROOT_DIR%\package\windows\cmake\*.* %CONTENTS_DIR%\cmake
xcopy /Y /Q %ROOT_DIR%\cmake\bulldozer-step.cmake %CONTENTS_DIR%\cmake
xcopy /Y /Q %ROOT_DIR%\include\*.* %CONTENTS_DIR%\include
xcopy /Y /Q %ROOT_DIR%\frame\*.* %CONTENTS_DIR%\frame
xcopy /Y /Q /E %ROOT_DIR%\samples\*.* %CONTENTS_DIR%\samples

set PREV_DIR=%CD%
cd %ZIP_DIR% || exit
%ZIP_EXE% a -tzip -y %OUT_FILE_PATH% || exit
cd %PREV_DIR% || exit

rd /S /Q "%ZIP_DIR%"

rem ...........................................................................
